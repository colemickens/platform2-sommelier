# Copyright 2019 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":dev_install",
  ]
  if (use.test) {
    deps += [ ":dev_install_test" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "vboot_host",
  ]
}

static_library("libdev_install") {
  configs += [ ":target_defaults" ]
  sources = [
    "dev_install.cc",
  ]
}

executable("dev_install") {
  configs += [ ":target_defaults" ]
  sources = [
    "dev_install_main.cc",
  ]
  deps = [
    ":libdev_install",
  ]
}

if (use.test) {
  pkg_config("test_config") {
    pkg_deps = [ "libchrome-test-${libbase_ver}" ]
  }

  executable("dev_install_test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
      ":test_config",
    ]
    sources = [
      "dev_install_test.cc",
    ]
    deps = [
      ":libdev_install",
      "//common-mk/testrunner",
    ]
  }
}
