# Copyright 2018 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":arc-adbd",
  ]

  if (use.fuzzer) {
    deps += [
      ":arc-adbd-create-pipe-fuzzer",
      ":arc-adbd-setup-config-fs-fuzzer",
      ":arc-adbd-setup-function-fs-fuzzer",
    ]
  }
}

pkg_config("config") {
  pkg_deps = [
    "libbrillo-${libbase_ver}",
    "libchrome-${libbase_ver}",
  ]
}

static_library("libarcadbd") {
  sources = [
    "adbd.cc",
    "adbd.h",
  ]
  configs += [ ":config" ]
}

executable("arc-adbd") {
  sources = [
    "adbd_main.cc",
  ]

  deps = [
    ":libarcadbd",
  ]
  configs += [ ":config" ]
}

# Fuzzer only.
if (use.fuzzer) {
  pkg_config("arc_adbd_fuzzer_config") {
    pkg_deps = [ "libchrome-test-${libbase_ver}" ]
  }

  executable("arc-adbd-setup-config-fs-fuzzer") {
    sources = [
      "fuzzers/adbd_setup_config_fs_fuzzer.cc",
    ]

    configs += [
      "//common-mk/common_fuzzer",
      ":arc_adbd_fuzzer_config",
    ]

    deps = [
      ":libarcadbd",
    ]
  }

  executable("arc-adbd-setup-function-fs-fuzzer") {
    sources = [
      "fuzzers/adbd_setup_function_fs_fuzzer.cc",
    ]

    configs += [
      "//common-mk/common_fuzzer",
      ":arc_adbd_fuzzer_config",
    ]

    deps = [
      ":libarcadbd",
    ]
  }

  executable("arc-adbd-create-pipe-fuzzer") {
    sources = [
      "fuzzers/adbd_create_pipe_fuzzer.cc",
    ]

    configs += [
      "//common-mk/common_fuzzer",
      ":arc_adbd_fuzzer_config",
    ]

    deps = [
      ":libarcadbd",
    ]
  }
}
